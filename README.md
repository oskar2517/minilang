# Simple programming language written in Haskell
This repository contains the source code and documentation of an interpreter for simple imperative programming language.

## Features
- If/While statements
- Block-scoped variables
- Arrays

## Syntax
The following grammar describes the syntax of this programming language in [EBNF format](https://en.wikipedia.org/wiki/Extended_Backus%E2%80%93Naur_form):
```ebnf
Digit = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "0";
Letter = "a" .. "z" | "A" .. "Z";

Statement = PrintStatement | VariableDeclarationStatement | VariableAssignStatement | ArrayAssignStatement | IfStatement | WhileStatement;
PrintStatement = "print", Expression, ";";
VariableDeclarationStatement = "var", Identifier, "=", Expression, ";";
VariableAssignStatement = Identifier, "=", Expression, ";";
ArrayAssignStatement = ArrayAccessExpression, "=", Expression, ";";
IfStatement = "if", Expression, BlockStatement, [ "else", BlockStatement ];
WhileStatement = "while", Expression, BlockStatement;
BlockStatement = "{", { Statement }, "}";

Identifier = ( Letter | "_" ) { ( Letter | Digit | "_" ) };

Expression = BinaryOperatorExpression | UnaryOperatorExpression | ParenExpression | Literal | ArrayAccessExpression;
BinaryOperatorExpression = Expression, BinaryOperator, Expression;
UnaryOperatorExpression = UnaryOperator, Expression;
BinaryOperator = "+" | "-" | "*" | "/" | "%" | "==" | "!=" | "<" | "<=" | ">" | ">=";
UnaryOperator = "-";
ExpressionList = [ ( Expression, { ",", Expression } ) ];
ParenExpression = "(", Expression, ")";

Literal = NumberLiteral | StringLiteral | BooleanLiteral | NilLiteral | ArrayLiteral;
NumberLiteral = IntegerLiteral | FloatLiteral;
IntegerLiteral = Digit, { Digit };
FloatLiteral = Digit, { Digit }, [ ".", Digit, { Digit } ];
StringLiteral = '"', { StringChar }, '"';
StringChar = <any character not '"'>;
BooleanLiteral = "true" | "false";
NilLiteral = "nil";
ArrayLiteral = "[", ExpressionList, "]";
ArrayAccessExpression = Expression, "[", Expression, "]";
```

## Example
Code for a basic program which calculates the modular multiplicative inverse of two numbers. For more examples, checkout [examples](examples).
```js
var a = 42;
var b = 2017;

var i = 0;
while i < b {
    if (a * i) % b == 1 {
        print i;
    }

    i = i + 1;
}
```

## Usage
A binary can be generated by executing `build.bat`. A program can executed using `interpreter.exe <path/to/file>`.

## Documentation
Click [here](docs/index.html) to view the code documentation. An updated documentation can be generated by executing `generateDocs.bat`.